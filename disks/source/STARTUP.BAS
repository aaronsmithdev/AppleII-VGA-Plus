 5  TEXT : GOSUB 1450: VTAB 13: HTAB 8: PRINT "[LOADING CONFIGURATION]"
 10 D$ =  CHR$ (4):SLOT = 0: GOTO 1100
 20 BASEADDR = 49280 + 16 * SLOT
 30  GOSUB 1450
 40  VTAB 10: GOSUB 1400: HTAB 4: PRINT "CONFIGURATION OPTIONS FOR SLOT ";SLOT;"": GOSUB 1400: PRINT ""
 50  PRINT "1  SCANLINES  ON      2  SCANLINES  OFF"
 60  PRINT "3  MONOCHROME ON      4  MONOCHROME OFF"
 70  PRINT "5  WHITE TEXT         6  GREEN TEXT"
 80  PRINT "7  AMBER TEXT         8  CHARACTER TEST"
 90  PRINT "9  RESET SLOT CONFIG  10 COLOR PATTERN"
 100  PRINT "11 ABOUT              12 QUIT": PRINT ""
 110  VTAB 21: GOSUB 1400: INPUT "PLEASE ENTER YOUR SELECTION: ";CH
 120  IF CH < 1 THEN  GOSUB 1550
 130  IF CH > 12 THEN  GOSUB 1550
 140  IF CH = 1 THEN  GOSUB 500
 150  IF CH = 2 THEN  GOSUB 550
 160  IF CH = 3 THEN  GOSUB 600
 170  IF CH = 4 THEN  GOSUB 650
 180  IF CH = 5 THEN  GOSUB 700
 190  IF CH = 6 THEN  GOSUB 750
 200  IF CH = 7 THEN  GOSUB 800
 210  IF CH = 8 THEN  GOSUB 900
 220  IF CH = 9 THEN  GOTO 1000
 230  IF CH = 10 THEN  GOSUB 850
 240  IF CH = 11 THEN  GOSUB 1650
 250  IF CH = 12 THEN  GOSUB 2000
 260  GOTO 20
 500  REM  '''SCANLINES ON
 510  POKE BASEADDR + 0,1: RETURN 
 550  REM  '''SCANLINES OFF
 560  POKE BASEADDR + 0,2: RETURN 
 600  REM  '''MONOCHROME ON
 610  POKE 49185,128: RETURN 
 650  REM  '''MONOCHROME OFF
 660  POKE 49185,0: RETURN 
 700  REM  '''MONOCHROME WHITE TEXT
 710  GOSUB 600
 720  POKE BASEADDR + 1,1
 730  RETURN 
 750  REM  '''MONOCHROME GREEN TEXT
 760  GOSUB 600
 770  POKE BASEADDR + 1,2
 780  RETURN 
 800  REM  '''MONOCHROME AMBER TEXT
 810  GOSUB 600
 820  POKE BASEADDR + 1,3
 830  RETURN 
 850  REM  '''COLOR PATTERN
 860  GR : HOME 
 870  FOR I = 0 TO 31: COLOR= I / 2: VLIN 0,39 AT I: NEXT I: FOR I = 0 TO 14 STEP 2: PRINT  TAB( I * 2 + 1);I;: NEXT I: PRINT : FOR I = 1 TO 15 STEP 2: PRINT  TAB( I * 2 + 1);I;: NEXT I
 880  PRINT "": GET A$: TEXT : RETURN 
 900  REM  '''CHARACTER SET TEST
 910  HOME 
 920  FOR R = 0 TO 7
 930  FOR C = 0 TO 31
 940  POKE R * 128 + 1024 + C,R * 32 + C
 950  NEXT C,R
 960  VTAB 21: GOSUB 1400: PRINT "PRESS ANY KEY TO CONTINUE...": GET A$
 970  RETURN 
 1000  REM  '''RESET SLOT CONFIGURATION
 1010  PRINT D$;"NOMON C,I,O": GOSUB 1450: VTAB 13: HTAB 7: PRINT "[RESETTING CONFIGURATION]": PRINT D$;"DELETE IIVGACONF": GOTO 10
 1100  REM  '''READ SLOT CONFIGURATION
 1105  PRINT D$;"NOMON C,I,O"
 1110  PRINT D$;"OPEN IIVGACONF"
 1120  PRINT D$;"READ IIVGACONF,R0"
 1130  ONERR  GOTO 1150
 1140  INPUT SLOT
 1150  PRINT D$;"CLOSE IIVGACONF"
 1170  IF SLOT <  > 0 THEN  GOTO 20
 1200  GOSUB 1450
 1220  VTAB 21: GOSUB 1400: INPUT "APPLE II VGA CARD SLOT (1-7): ";SLOT
 1230  IF SLOT < 1 THEN  GOTO 1500
 1240  IF SLOT > 7 THEN  GOTO 1500
 1300  REM  '''WRITE SLOT CONFIGURATION
 1310  PRINT D$;"NOMON C,I,O"
 1320  PRINT D$;"OPEN IIVGACONF"
 1330  PRINT D$;"WRITE IIVGACONF,R0"
 1340  PRINT SLOT
 1350  PRINT D$;"CLOSE IIVGACONF"
 1370  GOTO 20
 1400  REM  STRING ROUTINES
 1410  PRINT "---------------------------------------": RETURN 
 1450  HOME : PRINT "APPLE II VGA": GOSUB 1400: PRINT "COPYRIGHT (C) 2021-2023 MARK AIKENS": PRINT "COPYRIGHT (C) 2022-2023 DAVID KUDER": RETURN 
 1500  REM  '''ERROR HANDLING ROUTINES
 1510  GOSUB 1450: VTAB 13: HTAB 13: PRINT "[INVALID SLOT]"
 1520  GOSUB 1600: GOTO 1200
 1550  GOSUB 1450: VTAB 13: HTAB 11: PRINT "[INVALID OPTION]"
 1560  GOSUB 1600: RETURN 
 1600  FOR I = 1 TO 1000: NEXT I: RETURN 
 1650  GOSUB 1450: PRINT "": PRINT "": PRINT "THIS PROJECT IS A VGA CARD FOR APPLE II": PRINT "COMPUTERS TO  OUPUT A CRISP  RGB SIGNAL": PRINT "TO A VGA MONITOR  INSTEAD OF  HAVING TO": PRINT "RELY ON  THE COMPOSITE OUTPUT.  THIS IS"
 1660  PRINT "ACCOMPLISHED BY  SNOOPING THE  6502 BUS": PRINT "AND CREATING A SHADOW COPY OF THE VIDEO": PRINT "MEMORY WITHIN A RASPBERRY PI PICO, THEN": PRINT "PROCESSING THE VIDEO MEMORY CONTENTS TO"
 1670  PRINT "OUTPUT A 'PERFECT' SIGNAL.": PRINT "": PRINT "": HTAB 6: PRINT "[MORE DETAILS AVAILABLE AT]": PRINT "HTTPS://GITHUB.COM/MARKADEV/APPLEII-VGA"
 1680  VTAB 21: GOSUB 1400: PRINT "PRESS ANY KEY TO CONTINUE...": GET A$
 1690  RETURN 
 2000  HOME : PRINT "YOU MAY NOW LOAD YOUR SOFTWARE USING": PRINT "PRODOS OR THE APPROPRIATE PR# COMMAND.": PRINT "SETTINGS WILL PERSIST UNTIL THE SYSTEM": PRINT "IS REBOOTED.": END